TITLE:: FluidRobustScale
summary:: Apply Robust Scaling to FluidDataSet
categories:: FluidManipulation
related:: Classes/FluidStandardize, Classes/FluidNormalize, Classes/FluidDataSet

DESCRIPTION::
Scales the entries of a link::Classes/FluidDataSet::, or scales a data point according to the learned values of a data set. It will centre the median of each dimension to 0, and will scale the data to +/- the provided centiles, by default providing the first and third qartile (25 and 75).All happens on the server.

See https://scikit-learn.org/stable/auto_examples/preprocessing/plot_all_scaling.html#robustscaler

CLASSMETHODS::

private:: kr

METHOD:: new
Create a new instance
ARGUMENT:: server
The link::Classes/Server:: on which to run
ARGUMENT:: low
The low centile boundary, default 25.
ARGUMENT:: high
The high centile boundary, default 75.
ARGUMENT:: invert
The direction in which the scaling will occur for transform and transformpoint. The default 0 is taking in the range of the input used to fit and transforms it towards the robust scaling range. A value of 1 will expect an input of the scaling range to transform back to the original range.


INSTANCEMETHODS::

METHOD:: fit
Compute the scaling factors from a link::Classes/FluidDataSet:: for later.
ARGUMENT:: dataSet
The link::Classes/FluidDataSet:: to scale
ARGUMENT:: action
A function to run when processing is complete

METHOD:: transform
Scale a link::Classes/FluidDataSet:: into another link::Classes/FluidDataSet::, using the learned range from a previous call to link::Classes/FluidRobustScale#fit::
ARGUMENT:: sourceDataSet
The link::Classes/FluidDataSet:: to scale
ARGUMENT:: destDataSet
The link::Classes/FluidDataSet:: to populate with scaled data
ARGUMENT:: action
A function to run when processing is complete


METHOD:: fitTransform
Scale a link::Classes/FluidDataSet::
ARGUMENT:: sourceDataSet
The link::Classes/FluidDataSet:: to scale
ARGUMENT:: destDataSet
The link::Classes/FluidDataSet:: to populate with scaled data
ARGUMENT:: action
A function to run when processing is complete


METHOD:: transformPoint
Scale a new data point, using the learned scaling from a previous call to link::Classes/FluidRobustScale#fit::
ARGUMENT:: sourceBuffer
A link::Classes/Buffer:: with the new data point
ARGUMENT:: destBuffer
A link::Classes/Buffer:: to contain the scaled value
ARGUMENT:: action
A function to run when processing is complete

EXAMPLES::
code::
s.boot;
//Preliminaries: we want some audio, a couple of FluidDataSets, some Buffers and a FluidRobustScale
(
~audiofile = File.realpath(FluidBufPitch.class.filenameSymbol).dirname +/+ "../AudioFiles/Tremblay-ASWINE-ScratchySynth-M.wav";
~raw = FluidDataSet(s);
~scaled = FluidDataSet(s);
~pitch_feature = Buffer.new(s);
~stats = Buffer.alloc(s, 7, 2);
~robust = FluidRobustScale(s);
)

// Load audio and run a pitch analysis, which gives us pitch and pitch confidence (so a 2D datum)
(
~audio = Buffer.read(s,~audiofile);
FluidBufPitch.process(s,~audio, features: ~pitch_feature);
)

// Divide the time series in to 10, and take the mean of each segment and add this as a point to
// the 'raw' FluidDataSet
(
{
	var trig = LocalIn.kr(1, 1);
	var buf =  LocalBuf(2, 1);
    var count = PulseCount.kr(trig) - 1;
	var chunkLen = (~pitch_feature.numFrames / 10).asInteger;
	var stats = FluidBufStats.kr(
		source: ~pitch_feature, startFrame: count * chunkLen,
        numFrames: chunkLen, stats: ~stats, trig: (trig * (count <=9)), blocking:1
	);
	var rd = BufRd.kr(2, ~stats, DC.kr(0), 0, 1);// pick only mean pitch and confidence
	var wr1 = BufWr.kr(rd[0], buf, DC.kr(0));
	var wr2 = BufWr.kr(rd[1], buf, DC.kr(1));
	var dsWr = FluidDataSetWr.kr(~raw, buf: buf, trig: Done.kr(stats));
	LocalOut.kr( Done.kr(dsWr));
    Poll.kr(trig,count,\count);
	FreeSelf.kr(count - 9);
}.play;
)

//check the dataset
~raw.print;

// Scale and load to language-side array
(
~rawarray = Array.new(10);
~scaledarray= Array.new(10);
~robust.fitTransform(~raw,~scaled, {
	~raw.dump{|x| 10.do{|i|
		~rawarray.add(x["data"][i.asString])
	}};
	~scaled.dump{|x| 10.do{|i|
		~scaledarray.add(x["data"][i.asString])
	}};
});
)

//Plot side by side. Before normalization the two dimensions have radically different scales
//which can be unhelpful in many cases

(
(~rawarray ++ 0).flop.plot("Raw Data",Rect(0,0,400,400),minval:0,maxval:[5000,1]).plotMode=\bars;
(~scaledarray  ++ 0).flop.plot("Scaled",Rect(410,0,400,400), minval:-2,maxval:2).plotMode=\bars;
)

// single point transform on arbitrary value
~inbuf = Buffer.loadCollection(s,0.5.dup);
~outbuf = Buffer.new(s);
~robust.transformPoint(~inbuf,~outbuf,{|x|x.postln;x.getn(0,2,{|y|y.postln;};)});

//Server side queries
(
{
	var audio = BufRd.ar(1,~audio,LFSaw.ar(BufDur.ir(~audio).reciprocal).range(0, BufFrames.ir(~audio)));
	var counter = Stepper.ar(Impulse.ar(ControlRate.ir),max:99);
    var trig = A2K.kr(HPZ1.ar(counter) < 0);
	//average 100 frames: one could use the MovingAverage extension here
	var avg;
    var inputPoint = LocalBuf(2);
    var outputPoint = LocalBuf(2);
    var avgBuf = LocalBuf(100,2);
    //running average of pitch features
	BufWr.kr(FluidPitch.kr(audio),avgBuf,phase:counter);
	avg = Mix.new(BufRd.kr(2, avgBuf, phase:100.collect{|x|x})) * 0.01;
	//assemble data point
	BufWr.kr(avg[0],inputPoint,0);
	BufWr.kr(avg[1],inputPoint,1);
    ~robust.kr(trig,inputPoint,outputPoint);
	Poll.kr(trig,BufRd.kr(1,inputPoint,[0,1]),["pitch (raw)", "confidence (raw)"]);
    Poll.kr(trig,BufRd.kr(1,outputPoint,[0,1]),["pitch (scaled)", "confidence (scaled)"])
}.play;
)
::
